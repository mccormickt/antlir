# @oss-disable[end= ]: load("@fbsource//tools/target_determinator/macros:ci.bzl", "ci")
load("//antlir/antlir2/bzl/feature:defs.bzl", "feature")
load("//antlir/antlir2/bzl/image:defs.bzl", "image")
load("//antlir/antlir2/bzl/package:oci.bzl", "oci")
load("//antlir/antlir2/testing:image_test.bzl", "image_python_test")

oncall("antlir")

image.layer(
    name = "base",
    features = [
        feature.rpms_install(rpms = ["coreutils"]),
        feature.install_text(
            dst = "/to-be-deleted",
            text = "to-be-deleted",
        ),
        feature.install_text(
            dst = "/to-be-deleted-and-recreated",
            text = "to-be-deleted-and-recreated",
        ),
    ],
)

image.layer(
    name = "intermediate",
    features = [
        feature.remove(path = "/to-be-deleted-and-recreated"),
    ],
    parent_layer = ":base",
)

image.layer(
    name = "layer",
    features = [
        # make sure that RPM installations can be layered
        feature.rpms_install(rpms = ["bash"]),
        feature.remove(path = "/to-be-deleted"),
        feature.install_text(
            dst = "/to-be-deleted-and-recreated",
            text = "recreated\n",
        ),
        feature.install_text(
            dst = "/entrypoint.sh",
            mode = "a+rx",
            text = """#!/bin/bash
if [ "$1" != "foo" ]; then
    echo "Expected $1=foo"
fi
echo "Entrypoint!"
stat --format="%a %u %g" /entrypoint.sh
stat /to-be-deleted 2>&1 || true
cat /to-be-deleted-and-recreated
""",
        ),
    ],
    parent_layer = ":intermediate",
)

oci(
    name = "oci",
    entrypoint = [
        "/entrypoint.sh",
        "foo",
    ],
    layer = ":layer",
)

image.layer(
    name = "test-layer",
    features = [
        feature.rpms_install(rpms = [
            "podman",
            "python3",
        ]),
    ],
)

image_python_test(
    name = "test",
    srcs = ["test.py"],
    env = {
        "OCI": "$(location :oci)",
    },
    # This test does not work under architecture emulation. Mark it as such in
    # buck-land, and also disable scheduling CI for aarch64
    exec_compatible_with = select({
        "ovr_config//cpu:arm64": ["ovr_config//cpu:arm64"],
        "ovr_config//cpu:x86_64": ["ovr_config//cpu:x86_64"],
    }),
    # @oss-disable[end= ]: labels = ci.labels(ci.remove(ci.aarch64())),
    layer = ":test-layer",
)
